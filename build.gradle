//Import plugins needed to build and test the project.
plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'org.jetbrains.intellij' version "0.2.8"
    id 'checkstyle'
    id 'findbugs'
    id 'java'
    id 'idea'
}

//Declare project version.
version = pluginVersion

//Get dependencies from Maven central repository.
repositories {
    mavenCentral()
}


//Download Intellij sources to test the plugin.
intellij {
    version ideaVersion
    plugins 'yaml'
    downloadSources Boolean.valueOf(sources)
}

//Add required variables to build plugin (version, since-build, until-build)
patchPluginXml {
    sinceBuild = ideaBranch
    untilBuild = '*.*'
    version = pluginVersion
}

//Declare project dependencies
dependencies {
    compile("io.cloudslang.lang:cloudslang-entities:${cloudslangVersion}") {
        transitive = false
    }
    compile("io.cloudslang.lang:cloudslang-compiler:${cloudslangVersion}") {
        transitive = false
    }
    compile 'org.apache.commons:commons-collections4:4.1'
    compile ('com.googlecode.lambdaj:lambdaj:2.3.3') {
        transitive = false
    }
    compile "io.cloudslang.lang:cloudslang-spi:${cloudslangVersion}"
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'commons-logging:commons-logging:1.2'
    compile 'org.javassist:javassist:3.20.0-GA'
    compile 'org.hamcrest:hamcrest-all:1.1'
    compile 'org.objenesis:objenesis:1.2'
    compile 'commons-io:commons-io:2.4'
    compile 'org.yaml:snakeyaml:1.16'
}

//Set the sources and target compatibility with Java 8 (or other versions that have to
// match the Java version used in Gradle as well)
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

//Perform the release while ignoring any unversioned files (as they are not needed).
release {
    failOnUnversionedFiles = false
}

//FindBugs support that uses a template which generates a report.
tasks.withType(FindBugs) {
    ignoreFailures = true
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/xsl/findbugs-custom.xsl')
    }
}

//CheckStyle support that uses a checkstyle xml file and generates a report based on a template.
tasks.withType(Checkstyle) {
    ignoreFailures = true
    showViolations = false
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }
}