//Import plugins needed to build and test the project.
plugins {
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'net.researchgate.release' version '2.6.0'
    id 'org.jetbrains.intellij' version "0.2.18"
}

apply plugin: 'findbugs'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'

//Declare project version.
version = '1.0.5-SNAPSHOT'

//Get dependencies from Maven central repository.
repositories {
    mavenCentral()
}

intellij {
    version '2016.3.4'
    plugins 'yaml'
    downloadSources = false
    updateSinceUntilBuild = false
}

dependencies {
    compile("io.cloudslang.lang:cloudslang-entities:1.0.34") {
        transitive = false
    }
    compile("io.cloudslang.lang:cloudslang-compiler:1.0.34") {
        transitive = false
    }
    compile 'org.apache.commons:commons-collections4:4.1'
    compile('com.googlecode.lambdaj:lambdaj:2.3.3') {
        transitive = false
    }
    compile "io.cloudslang.lang:cloudslang-spi:1.0.19"
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'commons-logging:commons-logging:1.2'
    compile 'org.javassist:javassist:3.20.0-GA'
    compile 'org.hamcrest:hamcrest-all:1.3'
    compile 'org.objenesis:objenesis:1.2'
    compile 'commons-io:commons-io:2.4'
    compile 'org.yaml:snakeyaml:1.16'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

//Perform the release while ignoring any unversioned files (as they are not needed).
release {
    failOnUnversionedFiles = false
    revertOnFail = true
}

defaultTasks 'clean', 'build'

//FindBugs support that uses a template which generates a report.
tasks.withType(FindBugs) {
    ignoreFailures = true
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/xsl/findbugs-custom.xsl')
    }
}

//CheckStyle support that uses a checkstyle xml file and generates a report based on a template.
tasks.withType(Checkstyle) {
    ignoreFailures = true
    showViolations = false
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }
}